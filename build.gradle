// Dependencies for the buildscript (not the program)
buildscript {
    repositories {
        mavenCentral()
        jcenter() // for shadow plugin
    }
}

plugins {
    id "java"
    id "application"
    id "jacoco"
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id "com.github.kt3k.coveralls" version "2.6.3"
    id 'com.palantir.git-version' version '0.5.2' // for the git version
}

apply from: 'gradle/jacoco.coverage.gradle'

// No Windows support
startScripts {
    doLast {
        delete windowsScript
    }
}

group = 'org.magicdgs'
version = '0.2.2-SNAPSHOT'
description = """Tools for sequencing barcoded read data (in FASTQ/BAM format)"""

def developer = "Daniel Gomez-Sanchez"
def contact = "daniel.gomez.sanchez@hotmail.es"

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = group + "." + rootProject.name.toLowerCase() + ".Main"

repositories {
    mavenCentral()
    // TODO: add artifactory when migration to GATK
    // TODO: Broad Institute artifactory should be removed when GATK4 is in Maven Central
    // maven {
    //    url "https://artifactory.broadinstitute.org/artifactory/libs-snapshot/" 
    // }
}

dependencies {
    // TODO: remove cli when migration to gatk framework
    compile 'commons-cli:commons-cli:1.3.1'
    // TODO: remove htsjdk when gatk migration
    compile 'com.github.samtools:htsjdk:2.2.1'
    // TODO: add the gatk framework for migration
    // TODO: they will do soon a master-SNAPSHOT, which we will use in the future (gatk/issue#1995)
    // compile group: 'org.broadinstitute', name: 'gatk', version: '4.alpha.1-225-g796497f-SNAPSHOT'
    // TODO: add the testng framwwork when migration is done
    // testCompile 'org.testng:testng:6.9.10'
    testCompile 'junit:junit:4.12'
}

// for managing the wrapper task
task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

// test task
tasks.withType(Test) {

    // tests could be always re-run
    outputs.upToDateWhen { false }

    // TODO: enable TestNG support (default is JUnit) when migration is done
    // useTestNG()

    // TODO: set a system property for the test JVM(s)
    // TODO: example -> systemProperty 'some.prop', 'value'

    // show the stdout/stderr of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // set heap size for the test JVM(s)
    minHeapSize = "1G"
    maxHeapSize = "2G"

    // TODO: set JVM arguments for the test JVM(s)
    // TODO: example -> jvmArgs '-XX:MaxPermSize=256m'

    // log the test that is running
    beforeTest { descriptor ->
        logger.lifecycle("Running Test: " +  descriptor)
    }

    // listen to standarrd out and standard error of the test JVM(s)
    def testOutput = ""
    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " standard out/err -> " + event.message)
    }

    // logging after the tests
    testLogging {
        testLogging {
            events "skipped", "failed"
            exceptionFormat = "full"
        }
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

processResources {
    def details = versionDetails()
    // this is better for having a "dirty" marker for versions
    def buildNumber = (gitVersion().contains("dirty")) ? details.gitHash + ".dirty" : details.gitHash;
    // get the timestamp in its format
    def date = new Date()
    def timestamp = date.format('yyyy-MM-dd HH:mm:ss')
    expand(name: project.name,
        version: project.version.replaceAll("-SNAPSHOT", ""),
        buildNumber: buildNumber,
        timestamp: timestamp,
        developer: developer,
        contact: contact) 
}

shadowJar {
    zip64 true
    classifier = null
    version = null
}
